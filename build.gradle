plugins {
    id 'java'
}

repositories {
    mavenCentral()
    jcenter()

    maven { url 'https://maven.issizler.club/repository/maven-snapshots/' } // Use maven-releases when we release
}

group 'club.issizler'
version '0.1.1'

sourceCompatibility = 1.8

configurations {
    fatJar
    compile.extendsFrom fatJar
}

dependencies {
    // Please update this to the latest version available!
    implementation "club.issizler.okyanus:api:0.1.0-SNAPSHOT"

    // Anything in Okyanus Runtime's fatJar dependencies and anything that
    // Minecraft/Fabric Loader uses internally should essentially be "free" to
    // use. Meaning no additional jar size cost. (Make sure you use the same versions)
    implementation 'com.electronwill.night-config:toml:3.4.0' // Our preferred configuration library
    implementation 'org.apache.logging.log4j:log4j-api:2.8.1' // getServer().getLogger() requires this

    // Discord library
    fatJar('net.dv8tion:JDA:3.8.3_464') {
        exclude module: 'opus-java'
    }
}

jar {
    from {
        configurations.fatJar.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }
}